" sensible.vim - Defaults everyone can agree on
" Maintainer:   Tim Pope <http://tpo.pe/>
" Version:      1.1

if exists('g:loaded_sensible') || &compatible
  finish
else
  let g:loaded_sensible = 1
endif

if has('autocmd')
  filetype plugin indent on
endif
if has('syntax') && !exists('g:syntax_on')
  syntax enable
endif

" Use :help 'option' to see the documentation for the given option.

set autoindent
set backspace=indent,eol,start
set complete-=i
set smarttab

set nrformats-=octal

set ttimeout
set ttimeoutlen=100

set incsearch
" Use <C-L> to clear the highlighting of :set hlsearch.
if maparg('<C-L>', 'n') ==# ''
  nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>
endif

set laststatus=2
set ruler
set wildmenu

if !&scrolloff
  set scrolloff=1
endif
if !&sidescrolloff
  set sidescrolloff=5
endif
set display+=lastline

if &encoding ==# 'latin1' && has('gui_running')
  set encoding=utf-8
endif

if &listchars ==# 'eol:$'
  set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
endif

if v:version > 703 || v:version == 703 && has("patch541")
  set formatoptions+=j " Delete comment character when joining commented lines
endif

if has('path_extra')
  setglobal tags-=./tags tags-=./tags; tags^=./tags;
endif

if &shell =~# 'fish$'
  set shell=/bin/bash
endif

set autoread

if &history < 1000
  set history=1000
endif
if &tabpagemax < 50
  set tabpagemax=50
endif
if !empty(&viminfo)
  set viminfo^=!
endif
set sessionoptions-=options

" Allow color schemes to do bright colors without forcing bold.
if &t_Co == 8 && $TERM !~# '^linux\|^Eterm'
  set t_Co=16
endif

" Load matchit.vim, but only if the user hasn't installed a newer version.
if !exists('g:loaded_matchit') && findfile('plugin/matchit.vim', &rtp) ==# ''
  runtime! macros/matchit.vim
endif

inoremap <C-U> <C-G>u<C-U>

" vim:set ft=vim et sw=2:

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ADDITIONAL CUSTOMIZATIONS ADDED BELOW
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Set a few more universally useful options
" """
set nu                              " Alwasy display line numbers
set ruler                           " Always show cursor position
set nobackup                        " Disable backups, writebackups, swapfiles
set nowb
set noswapfile
set showmatch                       " set show matching parenthesis
set smarttab                        " insert tabs on the start of a line
                                    " according to shiftwidth, not tabstop

"""
" Set leader to a comma; it's an easier reach than the backslash.
" Handle other common leader issues.
"""
let mapleader = ","

" Display Hidden Characters; lower-case L
nmap <leader>l :set list!<CR>

" Toggle line numbers; uppercase N, etc.
nnoremap <leader>N :setlocal number!<cr>

" Disable hlsearch results after search
nnoremap <leader>. :nohlsearch<CR><C-l>

" Set TAB (key) characters to use the arrow so they are easier to identify.
set listchars=tab:â–¸\ ,eol:$

"""
" Trim Trailing White Space on Save of any file type
" """
autocmd BufWritePre *.* :%s/\s\+$//e

"""
" THE STATUSLINE (Linux, Mac OS X)
" """
" Right-side of the Status Line
" *Buffer Number, Displays + (writeable) and - read-only, and File name
" *Displays filetype, file encoding.
" *Hooks for vim-fugitive: outputs current git branch of file
let &stl='%< [Buf: %n]%m %f ['
let &stl.='%{empty(&filetype)?"":&filetype.", "}'
"let &stl.='%{&fenc!=""?&fenc:&enc}] %{fugitive#statusline()}'

" Left-side of the Status Line; Displays:
" *Hexadecimal value of byte under cursor
" *Line Number x of y | Column Number
" *Verticle position of cursor: Top/Botom/Percentage
let &stl.='%=[0x%02.2B] [Line: %04l/%04L | Col: %c%V] (%P) '

" Tell Vim to always put a status line in, even if there is only one window
set laststatus=2
